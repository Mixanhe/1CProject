
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТабДок = Новый ТабличныйДокумент;
	
	ПечатьНаСервере(ТабДок,ПараметрКоманды);
	
	ТабДок.Показать("ГарантийныйТалон");
	
КонецПроцедуры

&НаСервере
Процедура ПечатьНаСервере(ТабДок, СсылкаНаДокумент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорНаПоставкуКомплектующих.Контрагент.Наименование КАК КонтрагентНаименование,
	|	ДоговорНаПоставкуКомплектующих.Дата КАК Дата,
	|	ДоговорНаПоставкуКомплектующих.Номер КАК Номер
	|ИЗ
	|	Документ.ДоговорНаПоставкуКомплектующих КАК ДоговорНаПоставкуКомплектующих
	|ГДЕ
	|	ДоговорНаПоставкуКомплектующих.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорНаПоставкуКомплектующихТовары.Количество КАК Количество,
	|	ДоговорНаПоставкуКомплектующихТовары.Номенклатура.Представление КАК НаименованиеТовара,
	|	ДоговорНаПоставкуКомплектующихТовары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.ДоговорНаПоставкуКомплектующих.Товары КАК ДоговорНаПоставкуКомплектующихТовары
	|ГДЕ
	|	ДоговорНаПоставкуКомплектующихТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	
	МассивРезультовЗапроса = Запрос.ВыполнитьПакет();
	
	РезультатДанныеШапки = МассивРезультовЗапроса[0];
	РезультатДанныеТовары = МассивРезультовЗапроса[1];
	
	ВыборкаШапка = РезультатДанныеШапки.Выбрать();
	ВыборкаШапка.Следующий();
	
	ВыборкаТовара = РезультатДанныеТовары.Выбрать();
	
	
	
	Макет = Документы.ДоговорНаПоставкуКомплектующих.ПолучитьМакет("ГарантийныйТалон");
	
	// шапка
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьШапка.Параметры.НомерТалона = ВыборкаШапка.Номер;
	ОбластьШапка.Параметры.НазваниеОрганизации = ВыборкаШапка.КонтрагентНаименование;
	ТабДок.Вывести(ОбластьШапка);
	
	// заголовок таблицы
	ОбластьЗаголовокТалицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДок.Вывести(ОбластьЗаголовокТалицы);
	
	// товары
	КоличествоИтог = 0;
	Пока ВыборкаТовара.Следующий() Цикл
		
		ОбластьТовары =  Макет.ПолучитьОбласть("Товары");
		ОбластьТовары.Параметры.НомерСтроки = ВыборкаТовара.НомерСтроки;
		ОбластьТовары.Параметры.Количество = ВыборкаТовара.Количество;
		ОбластьТовары.Параметры.НаименованиеТовара = ВыборкаТовара.НаименованиеТовара;
		КоличествоИтог = КоличествоИтог + ВыборкаТовара.Количество;
		
		ТабДок.Вывести(ОбластьТовары);
		
	КонецЦикла; 
	
	// товары итог
	ОбластьТоварыИтог =  Макет.ПолучитьОбласть("ТоварыИтоги");
	ОбластьТоварыИтог.Параметры.КоличествоИтог = КоличествоИтог;
	ТабДок.Вывести(ОбластьТоварыИтог);
	
	// текст соглашения
	ОбластьТекстСоглашения =  Макет.ПолучитьОбласть("ТекстСоглашения");
	ОбластьТекстСоглашения.Параметры.ДатаВыдачи = Формат(ВыборкаШапка.Дата,"ДФ=dd.MM.yyyy");
	ТабДок.Вывести(ОбластьТекстСоглашения);
	
КонецПроцедуры

