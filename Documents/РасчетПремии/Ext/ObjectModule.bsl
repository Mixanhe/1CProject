
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ЗаработнаяПлата
	Движения.ЗаработнаяПлата.Записывать = Истина;
	Для Каждого ТекСтрокаСписокСотрудников Из СписокСотрудников Цикл
		Движение = Движения.ЗаработнаяПлата.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ВидРасчета;
		Движение.ПериодДействияНачало = НачалоМесяца(Дата);
		Движение.ПериодДействияКонец = КонецМесяца(Дата);
		Движение.ПериодРегистрации = Дата;
		Движение.БазовыйПериодНачало = ТекСтрокаСписокСотрудников.БазаНачало;
		Движение.БазовыйПериодКонец = КонецДня(ТекСтрокаСписокСотрудников.БазаКонец);
		Движение.Сотрудник = ТекСтрокаСписокСотрудников.ФИО;
		Движение.ДанныеДляРасчета = ТекСтрокаСписокСотрудников.Процент;
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаработнаяПлатаБазаЗаработнаяПлата.СуммаБаза КАК СуммаБаза,
	|	ЗаработнаяПлатаБазаЗаработнаяПлата.ДанныеДляРасчета КАК ДанныеДляРасчета,
	|	ЗаработнаяПлатаБазаЗаработнаяПлата.Сотрудник КАК Сотрудник
	|ИЗ
	|	РегистрРасчета.ЗаработнаяПлата.БазаЗаработнаяПлата(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК ЗаработнаяПлатаБазаЗаработнаяПлата";
	
	Измерения = Новый Массив();
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Движения.ДолгиПоЗаработнойПлатеСотрудникам.Записывать = Истина;
		
		НаборЗаписей = РегистрыРасчета.ЗаработнаяПлата.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Ссылка);
		НаборЗаписей.Прочитать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Для Каждого Запись ИЗ НаборЗаписей Цикл
				
				Если Запись.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник Тогда
					
					// формула расчета премии для сотрудника
					Премия = ВыборкаДетальныеЗаписи.СуммаБаза * ВыборкаДетальныеЗаписи.ДанныеДляРасчета / 100;
					Запись.Сумма = Премия;
					
					// прерваем цикл, так как обьект, которому надо установить премию найден, осталось его просто записать
					// и переместить итератор внешнего цикла на следующий.
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			// регистр долги по зпшке приход
			Движение = Движения.ДолгиПоЗаработнойПлатеСотрудникам.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
			Движение.Сумма = Премия;
			
		КонецЦикла;
		
		// запишу движения по регистру зарплаты.
		НаборЗаписей.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры



